prompt -----------------------------------------------------------------------;
prompt Creation of triggers for Invoice_Items_Adjust;
prompt -----------------------------------------------------------------------;

create or replace trigger biud_inv_item_adj
before insert or update or delete on invoice_item_adjust   
   
  begin 
  
     pkg_inv_trig.v_ind_inv_item_adj := 0;
     pkg_inv_trig.v_ind_inv_item_adj_sort := 0;
     pkg_inv_trig.v_inv_item_adj_parent_id.delete;
     pkg_inv_trig.v_inv_item_adj_parent_id_dist.delete;
  
   end biud_inv_item_adj;
   
/
show errors;


create or replace trigger biu_inv_item_adj_row
before insert or update on invoice_item_adjust
for each row

  declare
  
  begin 

    if :new.adjustment_amount is null and :new.plan_paid is null and :new.writeoff_amount is null then
      :new.net_adjust := null;
    else
      :new.net_adjust := 0 - nvl(:new.adjustment_amount, 0) - nvl(:new.plan_paid, 0) - nvl(:new.writeoff_amount, 0);
    end if;
 
   end biu_inv_item_adj_row;
/
show errors;


create or replace trigger aiud_inv_item_adj_row
after insert or update or delete on invoice_item_adjust
for each row

  declare
  
  begin 

    pkg_inv_trig.v_ind_inv_item_adj := pkg_inv_trig.v_ind_inv_item_adj + 1;
    
    if deleting then
      pkg_inv_trig.v_inv_item_adj_parent_id(pkg_inv_trig.v_ind_inv_item_adj) := :old.parent_id;
     elsif inserting or updating then
       pkg_inv_trig.v_inv_item_adj_parent_id(pkg_inv_trig.v_ind_inv_item_adj) := :new.parent_id; 
     end if;
 
   end aiud_inv_item_adj_row;
/
show errors;
   

create or replace trigger aiud_inv_item_adj
after insert or update or delete on invoice_item_adjust   
   
  declare
    v_parent_id invoice_item_adjust.parent_id%type;
    v_total_adjust invoice_item.total_adjust%type;
    
  begin 
  
     pkg_inv_trig.sort_inv_item_adj; 
  
     for i in 1..pkg_inv_trig.v_ind_inv_item_adj_sort loop
       v_parent_id := pkg_inv_trig.v_inv_item_adj_parent_id_dist(i);
       select sum(net_adjust) into v_total_adjust from invoice_item_adjust where parent_id=v_parent_id;
       
       update invoice_item set total_adjust = v_total_adjust,
                               balance = extended_cost + v_total_adjust
          where item_id = v_parent_id;
          
     end loop; 
 
   end aiud_inv_item_adj;
   
/
show errors;